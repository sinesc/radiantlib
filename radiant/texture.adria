/*
 * radiant 2d webgl rendering engine
 * Copyright (C) 2014 Dennis MÃ¶hlmann <mail@dennismoehlmann.de>
 * Licensed under the MIT license.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of
 * this software and associated documentation files (the "Software"), to deal in
 * the Software without restriction, including without limitation the rights to
 * use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of
 * the Software, and to permit persons to whom the Software is furnished to do so,
 * subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS
 * FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR
 * COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER
 * IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN
 * CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 */
module Texture = proto {

    radiant : null,
    canvas  : null,
    handle  : null,
    width   : 0,
    height  : 0,

    constructor: func(object radiant, object source) {

        this.radiant = radiant;
        this.store(source);
        this.generate();
        this.radiant.on('contextrestored', this, this.generate);
    },

    destructor: func() {

        this.radiant = null;
        this.canvas = null;
        this.handle = null;
    },

    /**
     * create power of 2 sized canvas from image source
     *
     * @param object source
     */
    store: func(object source) {

        this.width = source.width;
        this.height = source.height;

        this.canvas = document.createElement('canvas');
        this.canvas.width = nextPowerOfTwo(source.width);
        this.canvas.height = nextPowerOfTwo(source.height);
        var ctx = this.canvas.getContext('2d');
        ctx.drawImage(source, 0, 0);
    },

    /**
     * (re)creates texture object from current canvas
     */
    generate: func() {

        var gl = this.radiant.gl;

        this.handle = gl.createTexture();
        gl.activeTexture(gl.TEXTURE0);
        gl.bindTexture(gl.TEXTURE_2D, this.handle);
        gl.texImage2D(gl.TEXTURE_2D, 0, gl.RGBA, gl.RGBA, gl.UNSIGNED_BYTE, this.canvas);
        gl.texParameteri(gl.TEXTURE_2D, gl.TEXTURE_MAG_FILTER, gl.LINEAR);
        //gl.texParameteri(gl.TEXTURE_2D, gl.TEXTURE_MIN_FILTER, gl.LINEAR_MIPMAP_NEAREST);
        //gl.generateMipmap(gl.TEXTURE_2D);
        gl.texParameteri(gl.TEXTURE_2D, gl.TEXTURE_MIN_FILTER, gl.LINEAR);
        //gl.texParameteri(gl.TEXTURE_2D, gl.TEXTURE_WRAP_S, gl.CLAMP_TO_EDGE);
        //gl.texParameteri(gl.TEXTURE_2D, gl.TEXTURE_WRAP_T, gl.CLAMP_TO_EDGE);
    },

    /**
     * select this texture for given texture unit
     */
    use: func(finite unit = 0) {
        var gl = this.radiant.gl;
        gl.activeTexture(gl.TEXTURE0 + unit);
        gl.bindTexture(gl.TEXTURE_2D, this.handle);
    },

    /**
     * converts array of given x/y coodinate into texture uv coordinate
     *
     * @param finite x
     * @param finite y
     * @return Array u, v
     */
    getUV: func(finite x, finite y) {
        return [ x / this.canvas.width, y / this.canvas.height ];
    },
};

var nextPowerOfTwo = func(value) {
    return Math.pow(2, Math.ceil(Math.log(value) / Math.LN2));
};